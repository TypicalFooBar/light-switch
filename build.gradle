apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.hidetake.ssh'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.2.0'
mainClassName = 'us.derekwebb.lightswitch.LightSwitch'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
  }
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'com.google.code.gson:gson:2.3.1'
	compile 'org.eclipse.jetty:jetty-servlet:9.3.0.v20150612'
	compile 'com.pi4j:pi4j-core:1.0'
	compile 'org.xerial:sqlite-jdbc:3.8.10.1'
}

applicationDistribution.from('www') {
	into 'www'
}

remotes {
	pi {
		host = '192.168.1.108'
		user = 'pi'
		password = 'raspberry'
		knownHosts = allowAnyHosts
	}
}

task deploy << {
	ssh.run {
		session(remotes.pi) {
			// Remove the current version
			executeSudo('rm -r /opt/light-switch-*', ignoreError: true)
			
			// Move the tar to the raspberry pi
			put from: 'build/distributions/light-switch-' + version + '.tar', into: '/home/pi/light-switch-' + version + '.tar'
			
			// Extract the tar
			executeSudo('tar -C /opt -xf /home/pi/light-switch-' + version + '.tar')
			
			// Delete the tar
			execute('rm /home/pi/light-switch-' + version + '.tar')
			
			// Start the server (could be replaced with a run.sh script)
			execute('cd /opt/light-switch-' + version + '; sudo bin/light-switch > /dev/null 2>&1 &')
		}
	}
}